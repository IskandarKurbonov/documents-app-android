// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.NDK_VERSION = "21.3.6528147"

    ext.kotlin_version = '1.4.30'

    ext.moxy_version = '2.2.1'

    repositories {
        google()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:4.3.8"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.6.1'
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task buildAar {
    childProjects.each { set ->
        if (set.key == "libtoolkit" || set.key == "appmanager") {
            return
        }

        dependsOn "${set.key}:assembleRelease"

        doLast {
            def lib = new File("${set.value.projectDir.path}/build/outputs/aar/${set.key}-release.aar")

            copy {
                from lib
                into "${getRootDir().parent}/libs"
            }
        }
    }
}